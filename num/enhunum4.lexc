!!!enhunum3.lexc!!!
! harmadfél és társai

Multichar_Symbols  +Num

+Dis +Ess +Soc +Tem +Tmp +Ik +Iadj +Kas +Npref+ +Par
+Posss1s +Posss2s +Posss3s +Possp1s +Possp2s +Possp3s 
+Posss1p +Posss2p +Posss3p +Possp1p +Possp2p +Possp3p

+Plur +Fam +Gens +Genpl
      
+Abl +Acc +Ade +All +Cau +Dat +Del
+Ela +Fac +For +Forp +Forpen +Ill +Ine +Ins +Nom
+Sub +Sup +Ter
      
§EOA§ §uue§ §ooez3§ §OEOE1§ §UUE§ §OEOE§ §AE§ §OEOE2§
§UUE1§ §oeoez1§ §oooe§ §oeoez2§ §AAA§ §oeoe§ §AA§ §oeoez§ §AE1§
§VV§ §b§ §h§ §k§ §n§ §r§ §t§ 
§aeoenr§ §oeanr§ §JPnull§
§Jnull§ 

      
LEXICON Root
NumPref2;
Num;

Lexicon NumPref2
+Npref+:fél Num;
+Npref+:másfél Num;
+Npref+:másodfél Num;
+Npref+:harmadfél Num;
+Npref+:negyedfél Num;
+Npref+:negyed Num;
+Npref+:ötödfél Num;
+Npref+:hatodfél Num;
+Npref+:hetedfél Num;
+Npref+:nyolcadfél Num;
+Npref+:kilencedfél Num;
+Npref+:tizedfél Num;
+Npref+:sok Num;
+Npref+:kevés Num;
+Npref+:több Num;
+Npref+:kis Num1;
          Num;

LEXICON Num
száz AddNum;
ezer AddNum;
ezres AddNum;
millió  AddNum;
billió AddNum;
milliárd AddNum;
kvardillió AddNum;
kvadrilliárd AddNum;
százas AddNum;
ezres AddNum;
milliós  AddNum;
billiós AddNum;
milliárdos AddNum;
kvardilliós AddNum;
kvadrilliárdos AddNum;
század AddNum;
ezred AddNum;
milliomod AddNum;
billiomod AddNum;
milliárdod AddNum;
kvadrilliomod AddNum;
kvadrilliárdod AddNum;
százados AddNum;
ezredes AddNum;
milliomodos AddNum;
billiomodos AddNum;
milliárdodos AddNum;
trillió AddNum;
trilliárd AddNum;
trilliós AddNum;
kvadrilliomodos AddNum;
kvadrilliárdodos AddNum;

LEXICON Num1
millió  AddNum;

LEXICON AddNum
+Num:0     Poss;
+Num:0     Plur;
+Num:0     Fam;

LEXICON Poss
+Dis:^+cDis^§ooez3§nként    #;    ! anként, enként
+Soc:^+cSoc^§EOA§st§uue§l   #;    ! astul, estül
+Tem:^+cTem^§k§or             #;    ! kor
+Par:^+cPar^§aeoenr§d       Szor;  ! od,ed,öd
+Tmp:^+cTmp^sz§oeanr§r      Sub;  ! szor, szer, ször
+Posss1s:^+cPosss1s^§OEOE1§m   Fam;   ! m
+Posss2s:^+cPosss2s^§OEOE1§d   Fam;   ! d
+Posss3s:^+cPosss3s^§Jnull§§AE1§   Fam;   ! ja, je, a,e
+Possp1s:^+cPossp1s^§UUE§nk   Fam;   ! unk, ünk
+Possp2s:^+cPossp2s^§OEOE1§t§OEOE§k   Fam;   ! etek, atok
+Possp3s:^+cPossp3s^§Jnull§§UUE1§k   Fam;   ! ük, uk
+Posss1p:^+cPosss1p^§JPnull§§AE§im   Fam;   ! aim
+Posss2p:^+cPosss2p^§JPnull§§AE§id   Fam;   ! aid
+Posss3p:^+cPosss3p^§JPnull§§AE§i   Fam;   ! ai, jai, ei, jei
+Possp1p:^+cPossp1p^§JPnull§§AE§ink   Fam;   ! aink, eink
+Possp2p:^+cPossp2p^§JPnull§§AE§it§OEOE2§k   Fam;  ! aitok, eitek
+Possp3p:^+cPossp3p^§JPnull§§AE§ik   Fam;   !aik, jaik, eik, jeik
                           Fam;

LEXICON Plur
+Plur:^+cPlur^§oeoez1§k    Gen;
                            Gen;
        
LEXICON Fam
+Fam:^+cFam^ék         Gen;
                       Gen;

LEXICON Gen
+Gens:^+cGens^é          Case;
+Genpl:^+cGenpl^éi     Case;
                       Case;
 
LEXICON Case
+Abl:^+cAbl^§t§§oooe§l      #;  ! tól, től
+Acc:^+cAcc^§oeoez2§t       #;  ! ot, et, öt, t
+Ade:^+cAde^§n§§AAA§l       #;  ! nál, nél
+All:^+cAll^§h§§oeoe§z      #;  ! hoz, hez, höz
+Cau:^+cCau^ért             #;  ! ért
+Dat:^+cDat^§n§§AA§k        #;  ! nak, nek
+Del:^+cDel^§r§§oooe§l      #;  ! ról, ről
+Ess:^+cEss^§uue§l          #;    ! ul, ül
+Ela:^+cEla^§b§§oooe§l      #;  ! ból, ből
+Fac:^+cFac^§VV§§AAA§       #;  ! vá, vé
+Forp:^+ccForp^§k§épp          #;  ! ként, képp, képpen
+Forpen:^+cxForpen^§k§éppen          #;  ! ként, képp, képpen
+For:^+cFor^§k§ént          #;  ! ként, képp, képpen
+Ill:^+cIll^§b§§AA§         #;  ! ból, ből
+Ine:^+cIne^§b§§AA§n        #;  ! ban, ben
+Ins:^+cIns^§VV§§AA§l       #;  ! val, vel
+Nom:^+cNom^0               #;  ! 
+Sub:^+cSub^§r§§AA§         #;  ! ra, re
+Sup:^+cSup^§oeoez§n        #;  ! on, en, ön, n
+Ter:^+cTer^ig              #;  ! ig

LEXICON Szor
+Tmp:^+cTmp^sz§oeanr§r      Sub;  ! szor, szer, ször
+Ik:^+cIk^ik                #;  ! ik
                            #;

LEXICON Sub
+Sub:^+cSub^§r§§AA§         #;  ! ra, re
+Iadj:^+cIadj^i         #;  ! i
+Kas:^+cKas^§oeanr§s     #;  !as,es,ös képző
                            #;

